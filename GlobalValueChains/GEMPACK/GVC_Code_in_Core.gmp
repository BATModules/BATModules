!============================================================================

  File      : Core model code replaced for GVC module
  Author    : David Cui
  Date      : 03-03-2023 15:49:09
  Remarks   : MRIO module code based on GTAPv7-MRIO model and GTAP10.1 data.

 =============================================================================!

Variable (orig_level=1.0)(all,c,COMM)(all,aa,AGENTS)(all,s,REG)(all,d,GVC_REG)
    pamds(c,aa,s,d) # basic price of import c from s for use by agent aa in d #; 
Variable !(orig_level=VMAC)!(all,c,COMM)(all,aa,AGENTS)(all,s,REG)(all,d,GVC_REG)
    qamds(c,aa,s,d) # quantity of import c from s for use by agent aa in d #;
Update (all,c,COMM)(all,aa,AGENTS)(all,s,GVC_REG)(all,d,GVC_REG)
    VMAC(c,aa,s,d) = pcif(c,s,d) * qamds(c,aa,s,d);
Update (all,c,COMM)(all,aa,AGENTS)(all,s,GVC_REG)(all,d,GVC_REG)
    VMAB(c,aa,s,d) = pamds(c,aa,s,d) * qamds(c,aa,s,d);
Coefficient (ge 0)(all,c,COMM)(all,a,ACTS)(all,r,REG)
    VMFB(c,a,r) # expenditure on imported c by act. a in r at basic prices #;
Read
    VMFB from file GTAPDATA header "VMFB";
Coefficient (ge 0)(all,c,COMM)(all,a,ACTS)(all,r,REG)
    GVC_VMFB1(c,a,r) # expenditure on imported c by act. a in r at basic prices #;
Formula(initial) (all,c,COMM)(all,a,ACTS)(all,r,REG)
    GVC_VMFB1(c,a,r) = VMFB(c,a,r);
Update (all,c,COMM)(all,a,ACTS)(all,r,REG)
    GVC_VMFB1(c,a,r) = pms(c,r) * qfm(c,a,r); 	
Coefficient (ge 0)(all,c,COMM)(all,a,ACTS)(all,r,REG)
    GVC_VMFB2(c,a,r) # expenditure on imported c by act. a in r at basic prices #;
Formula (all,c,COMM)(all,a,ACTS)(all,r,REG)
    GVC_VMFB2(c,a,r) = sum{s,REG, VMAB(c,a,s,r)};
Formula (all,c,COMM)(all,a,ACTS)(all,r,REG)
    VMFB(c,a,r)=if(GVC_ACT=0, GVC_VMFB1(c,a,r))+ if(GVC_ACT=1,GVC_VMFB2(c,a,r));
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    VMGB(c,r) # government expenditure on imported c in r at basic prices #;
Read
    VMGB from file GTAPDATA header "VMGB";
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    GVC_VMGB1(c,r) # government expenditure on imported c in r at basic prices #;
Formula(initial) (all,c,COMM)(all,r,REG)
    GVC_VMGB1(c,r) = VMGB(c,r);
Update (all,c,COMM)(all,r,REG)
    GVC_VMGB1(c,r) = pms(c,r) * qgm(c,r); 
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    GVC_VMGB2(c,r) # government expenditure on imported c in r at basic prices #;
Formula (all,c,COMM)(all,r,REG)
    GVC_VMGB2(c,r) = sum{s,REG, VMAB(c,"govt",s,r)};
Formula (all,c,COMM)(all,r,REG)
    VMGB(c,r) = if(GVC_ACT=0, GVC_VMGB1(c,r))+if(GVC_ACT=1, GVC_VMGB2(c,r));
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    VMPB(c,r) # private household expenditure on imp. c in r at basic prices #;
Read
    VMPB from file GTAPDATA header "VMPB"; 
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    GVC_VMPB1(c,r) # private household expenditure on imp. c in r at basic prices #;
Formula(initial) (all,c,COMM)(all,r,REG)
    GVC_VMPB1(c,r) = VMPB(c,r);
Update (all,c,COMM)(all,r,REG)
    GVC_VMPB1(c,r)= pms(c,r) * qpm(c,r); 
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    GVC_VMPB2(c,r) # private household expenditure on imp. c in r at basic prices #;
Formula (all,c,COMM)(all,r,REG)
    GVC_VMPB2(c,r) = sum{s,REG, VMAB(c,"hhld",s,r)};
Formula (all,c,COMM)(all,r,REG)
    VMPB(c,r) = if(GVC_ACT=0, GVC_VMPB1(c,r))+if(GVC_ACT=1, GVC_VMPB2(c,r));
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    VMIB(c,r) # investment expenditure on imported c in r at basic prices #;
Read
    VMIB from file GTAPDATA header "VMIB";
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    GVC_VMIB1(c,r) # investment expenditure on imported c in r at basic prices #;
Formula(initial) (all,c,COMM)(all,r,REG)
    GVC_VMIB1(c,r) = VMIB(c,r);
Update (all,c,COMM)(all,r,REG)
    GVC_VMIB1(c,r) = pms(c,r) * qim(c,r);
Coefficient (ge 0)(all,c,COMM)(all,r,REG)
    GVC_VMIB2(c,r) # investment expenditure on imported c in r at basic prices #;
Formula (all,c,COMM)(all,r,REG)
    GVC_VMIB2(c,r) = sum{s,REG, VMAB(c,"invt",s,r)};
Formula (all,c,COMM)(all,r,REG)
    VMIB(c,r) = if(GVC_ACT=0, GVC_VMIB1(c,r))+if(GVC_ACT=1, GVC_VMIB2(c,r));
Coefficient (ge 0)(all,c,COMM)(all,s,REG)(all,d,REG)
    VCIF(c,s,d) 
    # value of imports of c from s to d at CIF prices (tradeables only) #;
Read
    VCIF from file GTAPDATA header "VCIF";
Coefficient (ge 0)(all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VCIF1(c,s,d) 
    # value of imports of c from s to d at CIF prices (tradeables only) #;
Formula(initial) (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VCIF1(c,s,d) = VCIF(c,s,d);
Update (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VCIF1(c,s,d) = pcif(c,s,d) * qxs(c,s,d);
Coefficient (ge 0)(all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VCIF2(c,s,d) 
    # value of imports of c from s to d at CIF prices (tradeables only) #;
Formula (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VCIF2(c,s,d) = sum{aa,AGENTS, VMAC(c,aa,s,d)} ;
Formula (all,c,COMM)(all,s,REG)(all,d,REG)
    VCIF(c,s,d)=if(GVC_ACT=0, GVC_VCIF1(c,s,d))+if(GVC_ACT=1, GVC_VCIF2(c,s,d));
Coefficient (ge 0)(all,c,COMM)(all,s,REG)(all,d,REG)
    VMSB(c,s,d) 
    # value of imports of c from s to d at domestic (basic) prices #;
Read
    VMSB from file GTAPDATA header "VMSB";
Coefficient (ge 0)(all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VMSB1(c,s,d);
Formula(initial) (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VMSB1(c,s,d) = VMSB(c,s,d);
Update (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VMSB1(c,s,d) = pmds(c,s,d) * qxs(c,s,d);
Coefficient (ge 0)(all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VMSB2(c,s,d);
Formula (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_VMSB2(c,s,d) = sum{aa,AGENTS, VMAB(c,aa,s,d)}; 
Formula (all,c,COMM)(all,s,REG)(all,d,REG)
    VMSB(c,s,d)=if(GVC_ACT=0, GVC_VMSB1(c,s,d))+if(GVC_ACT=1, GVC_VMSB2(c,s,d));
Coefficient (all,c,COMM)(all,aa,AGENTS)(all,s,REG)(all,d,REG)
    MATAX(c,aa,s,d) # agent-spec. import tariff revevenue on c from s to d #;
Formula (all,c,COMM)(all,aa,AGENTS)(all,s,REG)(all,d,REG)
    MATAX(c,aa,s,d) = VMAB(c,aa,s,d) - VMAC(c,aa,s,d);
Coefficient (all,c,COMM)(all,s,REG)(all,d,REG)
    MTAX(c,s,d) # tax rev from imports of c from source s to destination d #;
Coefficient (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_MTAX1(c,s,d) # tax rev from imports of c from source s to destination d #;
Formula (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_MTAX1(c,s,d) = VMSB(c,s,d) - VCIF(c,s,d);
Coefficient (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_MTAX2(c,s,d) # tax rev from imports of c from source s to destination d #;
Formula (all,c,COMM)(all,s,REG)(all,d,REG)
    GVC_MTAX2(c,s,d) = sum{aa,AGENTS, MATAX(c,aa,s,d)};
Formula (all,c,COMM)(all,s,REG)(all,d,REG)
    MTAX(c,s,d)=if(GVC_ACT=0, GVC_MTAX1(c,s,d))+if(GVC_ACT=1, GVC_MTAX2(c,s,d));
